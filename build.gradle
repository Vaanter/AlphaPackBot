/*
 *    Copyright 2020 Valentín Bolfík
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
  id "java"
  id "io.quarkus"
  id "com.github.ben-manes.versions" version "0.39.0"
  id "net.ltgt.errorprone" version "2.0.2"
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    name 'm2-dv8tion'
    url 'https://m2.dv8tion.net/releases'
  }
}

dependencies {
  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
  implementation "io.quarkus:quarkus-grpc"
  implementation "io.quarkus:quarkus-arc"

  implementation('net.dv8tion:JDA:4.3.0_293') {
    exclude module: 'opus-java'
  }

  implementation "redis.clients:jedis:4.0.0"

  implementation "com.google.guava:guava:31.0.1-jre"
  implementation "com.google.mug:mug:5.7"
  implementation "org.apache.commons:commons-lang3:3.12.0"
  errorprone("com.google.errorprone:error_prone_core:2.10.0")

  testImplementation "io.quarkus:quarkus-junit5"
}

group "com.vb.alphapackbot"
version "4.3.2"

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile) {
  options.errorprone {
    disableWarningsInGeneratedCode = true
    ignoreUnknownCheckNames = true
    errorproneArgs = ['-XepExcludedPaths:.*/build/.*']
  }
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

sourceSets {
  main.java.srcDirs = ["build/classes/java/quarkus-generated-sources/grpc", "src/main/java"]
}
